buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "0.1"
group = "io.github.tivj.skydome"
archivesBaseName = "SkyDome"

sourceCompatibility = targetCompatibility = 1.8

dependencies {
    provided files("libs/modcore.jar")
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false

    replaceIn "SkyDomeMain.java"
    replace "@MOD_VERSION@", project.version
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes(
                "FMLCorePlugin": "io.github.tivj.skydome.asm.tweaker.Tweaker",
                "ForceLoadAsMod": true,
                "FMLCorePluginContainsFMLMod": true,
                "ModSide": "CLIENT",
                'FMLAT': 'skydome_at.cfg'
        )
    }
}